apply plugin: 'com.android.application'
//wrap with try and catch so the build is working even if the signing stuff is missing
try {
    apply from: '../../../signing.gradle'
} catch (ex) {
}

android {
    compileSdkVersion setup.compileSdk
    buildToolsVersion setup.buildTools

    defaultConfig {
        minSdkVersion setup.minSdk
        targetSdkVersion setup.targetSdk
        versionCode release.versionCode
        versionName release.versionName

        setProperty("archivesBaseName", "ItemAnimators-v$versionName-c$versionCode")
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            try {
                signingConfig signingConfigs.debug
            } catch (ex) {
            }
            minifyEnabled false
        }
        release {
            try {
                signingConfig signingConfigs.release
            } catch (ex) {
            }
            zipAlignEnabled true
            minifyEnabled false
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation project(':library')
    implementation "androidx.appcompat:appcompat:${versions.androidXVersion}"
    implementation "androidx.cardview:cardview:${versions.androidXVersion}"
    implementation "androidx.recyclerview:recyclerview:${versions.androidXVersion}"

    //used to generate the drawer on the left
    //https://github.com/mikepenz/MaterialDrawer
    implementation('com.mikepenz:materialdrawer:5.9.3@aar') {
        transitive = true
        exclude module: "itemanimators"
        exclude module: "fastadapter"
        exclude group: "com.android.support"
    }

    // used to fill the RecyclerView with the DrawerItems
    // and provides single and multi selection, collapsable items
    // https://github.com/mikepenz/FastAdapter
    implementation('com.mikepenz:fastadapter:2.6.3@aar')
    implementation('com.mikepenz:fastadapter-commons:2.6.3@aar')

    //used to generate the Open Source section
    //https://github.com/mikepenz/AboutLibraries
    implementation('com.mikepenz:aboutlibraries:5.9.8@aar')

    //used to display the icons in the drawer
    //https://github.com/mikepenz/Android-Iconics
    implementation 'com.mikepenz:material-design-iconic-typeface:2.2.0.1@aar'

    //https://github.com/JakeWharton/butterknife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    //used to load the images in the ImageListSample
    //https://github.com/bumptech/glide
    implementation('com.github.bumptech.glide:glide:3.8.0') {
        exclude group: "com.android.support"
    }
}
